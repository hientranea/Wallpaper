plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.hientran.wallpaper'
    compileSdk 33

    defaultConfig {
        applicationId "com.hientran.wallpaper"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.hientran.wallpaper.TestRunner"

        buildConfigField 'String', 'BASE_URL', readValueFromLocalProperties('BASE_URL', '\"\"')
        buildConfigField 'String', 'PEXEL_API_KEY', readValueFromLocalProperties('PEXEL_API_KEY', '\"\"')
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
        resources.excludes.add("MANIFEST.MF")
    }
}

dependencies {
    implementation androidX.core
    implementation androidX.appcompat
    implementation androidX.fragment
    implementation androidX.paging
    implementation androidX.constrainlayout
    implementation androidX.lifecycleviewmodel

    implementation material.values()

    implementation navigation.values()

    implementation glide.glide
    annotationProcessor glide.compiler

    implementation di.hilt
    kapt di.hiltcompiler

    implementation coroutines.values()

    implementation retrofit.values()

    implementation okhttp.values()

    implementation dimens.values()

    implementation timber.values()

    testImplementation testing.junit.junit
    testImplementation testing.coroutines
    testImplementation testing.mockk.mockk
    testImplementation testing.mockk.agentjvm

    androidTestImplementation testing.junit.androidext
    androidTestImplementation testing.espresso
    debugImplementation testing.fragment
    androidTestImplementation testing.hilt
    kaptAndroidTest testing.hiltcompiler
    androidTestImplementation testing.navigation
    androidTestImplementation testing.mockk.android
    androidTestImplementation(testing.barista) {
        exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
}

kapt {
    correctErrorTypes = true
}

String readValueFromLocalProperties(String key, String defaultValue) {
    return project.localProperties.getProperty(key, defaultValue)
}
